#
# A theme based on skwp theme
#  * NVM/node info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#   Rajiv Tirumalareddy <r@jiv.me>

function prompt_vijar_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  # Get node information
  if (( $+functions[node-info] )); then
    node-info
  fi
}

function prompt_vijar_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_vijar_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_vijar_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
  else
    __PROMPT_vijar_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # git
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_vijar_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${__PROMPT_vijar_COLORS[5]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_vijar_COLORS[2]}●%f"
  zstyle ':prezto:module:git:info:modified' format "${__PROMPT_vijar_COLORS[4]}●%f"
  zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_vijar_COLORS[3]}●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)'

  # node info (nvm)
  zstyle ':prezto:module:node:info:version' format '[node - %v]'

  PROMPT="${__PROMPT_vijar_COLORS[3]}%n%f@${__PROMPT_vijar_COLORS[2]}%m%f ${__PROMPT_vijar_COLORS[5]}%~%f "'$git_info[prompt]'"$ "
  RPROMPT='%F{blue}${node_info[version]}'
}

prompt_vijar_setup "$@"
